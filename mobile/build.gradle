apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "tech.chiuwaiho.ssr"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        resConfigs rootProject.resConfigs
        ndkVersion rootProject.ndkVersion
        resValue 'string', 'applicationId', applicationId
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            versionNameSuffix '-nightly'
            signingConfig signingConfigs.debug
            proguardFiles 'r8-rules.pro'
            kotlinOptions {
                freeCompilerArgs = [
                        '-Xno-param-assertions',
                        '-Xno-call-assertions',
                        '-Xno-receiver-assertions',
                        '-Xmultifile-parts-inherit',
                ]
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            resValue 'string', 'applicationId', defaultConfig.applicationId + applicationIdSuffix
        }
    }
    signingConfigs {
        config {
            enableV3Signing true
            enableV4Signing false
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    packagingOptions {
        exclude '/META-INF/*.version'
        exclude '**/*.kotlin_*'
        exclude '/*.bin'
    }
    aaptOptions.additionalParameters "--preferred-density", "xxxhdpi"
    kotlinOptions.jvmTarget = javaVersion
    lintOptions.checkReleaseBuilds false
    dependenciesInfo.includeInApk false
}

androidExtensions.experimental true

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugarLibsVersion"
    implementation project(':core')
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.takisoft.preferencex:preferencex-simplemenu:1.1.0'
    implementation 'com.twofortyfouram:android-plugin-api-for-locale:1.0.4'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.4'
}
